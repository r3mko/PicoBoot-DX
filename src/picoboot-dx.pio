 ; Copyright (c) 2022 Maciej Kobus
 ; Copyright (c) 2025 Remko Kleinjan
 ;
 ; SPDX-License-Identifier: GPL-2.0-only

.define PUBLIC CS_PIN      4    ; U10 chip select
.define PUBLIC CLK_PIN     5    ; EXI clock signal

; Wait for one full CS pulse
.macro WAIT_CS pin
    wait 1 gpio \pin        ; CS HIGH
    wait 0 gpio \pin        ; CS LOW
.endmacro

; Wait for a CLK rising edge
.macro WAIT_RISING_EDGE pin
    wait 0 gpio \pin        ; CLK low
    wait 1 gpio \pin        ; CLK high
.endmacro

.program on_transfer

start:
    WAIT_CS CS_PIN              ; Wait for CS
    jmp x-- start
    irq set 0                   ; Set IRQ flag to start injecting payload

next_transfer:
.wrap_target
    WAIT_CS CS_PIN              ; Wait for CS
    irq set 0                   ; Let other SM inject the payload
    wait 1 gpio CLK_PIN         ; Hold until CLK goes HIGH
.wrap

.program clocked_output

start:
    set x, 31                   ; x - 32 bits we need to skip (address)
    wait 1 irq 0                ; Hold until SM0 tells us to start injecting

wait_bit:
    WAIT_RISING_EDGE CLK_PIN    ; Sample until rising edge on CLK line 
    jmp x-- wait_bit            ; Skip past address bytes
    mov x, y                    ; Copy y value to x, it represents number of bits in the transfer
    set pindirs 1               ; Set data line as output
    out pins, 1                 ; Output single IPL bit
    jmp x-- on_rising_edge      ; Jump back to next clock pulse wait routine

on_rising_edge:
    WAIT_RISING_EDGE CLK_PIN    ; Sample until rising edge on CLK line 

write_bit:
    out pins, 1                 ; Output single IPL bit
    jmp x-- on_rising_edge      ; Jump back to next clock pulse wait routine if there is more data to inject

    jmp !osre start             ; If there is more data in OSR, go back to start routine and wait for another transfer

    set pindirs 0               ; Leave data line floating so RTC-DOL can take over and load fonts etc.

disable_chip:
    jmp disable_chip            ; Stay in the loop forever until power cycle
